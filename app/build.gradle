apply plugin: 'com.android.application'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'com.google.gms.google-services'
apply plugin: 'com.google.firebase.crashlytics'
apply plugin: 'kotlin-kapt'
apply plugin: "androidx.navigation.safeargs.kotlin"

android {
    compileSdkVersion 29
    buildToolsVersion "29.0.3"
    defaultConfig {
        applicationId "pl.llp.aircasting"
        minSdkVersion 21
        targetSdkVersion 29
        versionCode 134
        versionName "2.0.16"
        testInstrumentationRunner "androidx.test.runner.AndroidJUnitRunner"

        // Read the API key from ./secure.properties into R.string.maps_api_key
        def secureProps = new Properties()
        if (file("../secure.properties").exists()) {
            file("../secure.properties")?.withInputStream { secureProps.load(it) }
        }
        resValue "string", "maps_api_key", (secureProps.getProperty("MAPS_API_KEY") ?: "")
        buildConfigField "String", "PLACES_API_KEY", (secureProps.getProperty("PLACES_API_KEY") ?: "\"\"")

        javaCompileOptions {
            annotationProcessorOptions {
                arguments += ["room.schemaLocation":
                                      "$projectDir/schemas".toString()]
            }
        }
    }
    buildTypes {
        debug {
            manifestPlaceholders = [crashlyticsCollectionEnabled:"false"]
        }

        release {
            manifestPlaceholders = [crashlyticsCollectionEnabled:"true"]
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }

    kotlinOptions {
        jvmTarget = "1.8"
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    implementation fileTree(dir: 'libs', include: ['*.jar'])
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8:$kotlin_version"
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-core:1.3.6'
    implementation 'org.jetbrains.kotlinx:kotlinx-coroutines-android:1.3.6'

    implementation 'androidx.appcompat:appcompat:1.0.2'
    implementation 'androidx.activity:activity:1.2.0-alpha05'
    implementation 'androidx.activity:activity-ktx:1.2.0-alpha05'
    implementation 'androidx.fragment:fragment:1.3.0-alpha05'
    implementation 'androidx.fragment:fragment-ktx:1.3.0-alpha05'
    implementation 'androidx.core:core-ktx:1.0.2'
    implementation 'androidx.constraintlayout:constraintlayout:1.1.3'
    implementation 'androidx.lifecycle:lifecycle-extensions:2.0.0'
    implementation 'androidx.fragment:fragment-ktx:1.2.4'
    implementation 'androidx.navigation:navigation-fragment-ktx:2.0.0'
    implementation 'androidx.navigation:navigation-ui-ktx:2.0.0'
    implementation "androidx.swiperefreshlayout:swiperefreshlayout:1.0.0"

    implementation "androidx.recyclerview:recyclerview:1.1.0"
    // for complex recycler views
    implementation "com.xwray:groupie:2.8.1"
    implementation "com.xwray:groupie-kotlin-android-extensions:2.8.1"

    implementation 'com.google.android.material:material:1.2.0'

    implementation "com.google.guava:guava:29.0-android"
    implementation 'org.greenrobot:eventbus:3.2.0'

    // Process phoenix - restarting app library
    implementation 'com.jakewharton:process-phoenix:2.0.0'

    def daggerVersion = 2.27
    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"
    kaptAndroidTest "com.google.dagger:dagger-compiler:$daggerVersion"

    // for http requests
    implementation 'com.squareup.retrofit2:retrofit:2.9.0'
    androidTestImplementation "com.squareup.retrofit2:retrofit-mock:2.9.0"
    implementation 'com.squareup.retrofit2:converter-gson:2.5.0'
    implementation 'com.squareup.okhttp3:logging-interceptor:3.8.0'
    androidTestImplementation "com.squareup.okhttp3:mockwebserver:3.8.0"
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.opencsv:opencsv:4.6'

    implementation 'com.google.android.gms:play-services-location:17.0.0'
    implementation 'com.google.android.libraries.maps:maps:3.1.0-beta'

    // for Google Places API
    implementation(name:'places-maps-sdk-3.1.0-beta', ext:'aar')
    implementation 'com.google.android.gms:play-services-gcm:17.0.0'
    implementation 'com.google.auto.value:auto-value-annotations:1.6.2'

    // for Firebase Crashlytics and Test Lab
    implementation platform('com.google.firebase:firebase-bom:28.2.0')
    implementation 'com.google.firebase:firebase-crashlytics-ktx'
    implementation 'com.google.firebase:firebase-analytics-ktx'

    implementation "androidx.room:room-runtime:2.2.5"
    kapt "android.arch.persistence.room:compiler:2.2.5"

    implementation 'commons-codec:commons-codec:1.14'

    implementation 'no.nordicsemi.android:ble:2.2.4'

    // charts
    implementation 'com.github.PhilJay:MPAndroidChart:v3.1.0'

    // for testing
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'androidx.test.ext:junit:1.1.0'
    androidTestImplementation 'androidx.test.espresso:espresso-core:3.1.1'
    androidTestImplementation 'androidx.test.espresso:espresso-intents:3.1.1'
    implementation 'androidx.test.espresso:espresso-idling-resource:3.1.1'

    // for mocking in tests
    def mockitoVersion = '2.6.3'
    androidTestImplementation "org.mockito:mockito-core:$mockitoVersion"
    androidTestImplementation "org.mockito:mockito-android:$mockitoVersion"
    androidTestImplementation 'com.nhaarman:mockito-kotlin:1.5.0'


    // Leak detection
//    debugImplementation 'com.squareup.leakcanary:leakcanary-android:2.6'
}
